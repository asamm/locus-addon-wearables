apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion Integer.valueOf(PARAM_COMPILE_SDK_VERSION)
    buildToolsVersion PARAM_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.valueOf(PARAM_WEAR_MIN_SDK_VERSION)
        targetSdkVersion Integer.valueOf(PARAM_WEAR_TARGET_SDK_VERSION)

        versionCode Integer.valueOf(PARAM_MAJOR_VERSION + PARAM_MINOR_VERSION + PARAM_PATCH_VERSION + PARAM_WEAR_VERSION_SUFFIX)
        versionName Integer.valueOf(PARAM_MAJOR_VERSION) + '.' +
                Integer.valueOf(PARAM_MINOR_VERSION) + '.' +
                Integer.valueOf(PARAM_PATCH_VERSION)
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // set lint in project
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    // signing of versions
    signingConfigs {
        debug {
            storeFile file(System.getenv('ANDROID_SIGN_DEBUG').split("\\|")[0])
            storePassword System.getenv('ANDROID_SIGN_DEBUG').split("\\|")[1]
            keyAlias System.getenv('ANDROID_SIGN_DEBUG').split("\\|")[2]
            keyPassword System.getenv('ANDROID_SIGN_DEBUG').split("\\|")[3]
        }
        release {
            storeFile file(System.getenv('ANDROID_SIGN_RELEASE').split("\\|")[0])
            storePassword System.getenv('ANDROID_SIGN_RELEASE').split("\\|")[1]
            keyAlias System.getenv('ANDROID_SIGN_RELEASE').split("\\|")[2]
            keyPassword System.getenv('ANDROID_SIGN_RELEASE').split("\\|")[3]
        }
    }

    // building task
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    // Kotlin
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + KOTLIN_VERSION

    implementation project(path: ':common')

    // add Google stuff
    implementation 'androidx.wear:wear:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.android.support:wearable:' + LIB_ANDROID_SUPPORT_WEARABLE
    compileOnly 'com.google.android.wearable:wearable:' + LIB_ANDROID_SUPPORT_WEARABLE
}
